---
title: "Voronoi与伪Voronoi Pattern的快速生成算法"
categories:
  - Noise
date:   2018-03-20 21:43:00 +0800
tags:
  - Graphics
---

<script type="text/javascript" async src="https://cdn.mathjax.org/mathjax/latest/MathJax.js?config=TeX-MML-AM_CHTML"> </script>

## Voronoi

给定平面中的 N 个采样点 (seeds)， 对于每个 seed：$$p_i$$， 平面中存在一个区域，区域中所有点到 $$p_i$$ 的距离，比到除 $$p_i$$ 外其他所有 seeds 的距离都近，那么这个区域被称为一个 voronoi cell。 这 N 个采样点，就按照最邻近原则划分了平面，如果读者接触过 Delaunay Triangulation 的话，Delauney 刚好是 Voronoi 的偶图。

Voronoi pattern 在 Procedural content 中常常作为 Noise 使用，这种连接的不规则区域可以让人联想到很多自然界中的表面，如长颈鹿的皮肤，蜻蜓翅膀的纹理，乌龟的壳，龟裂的土地等等。

## Standard Implementation

标准的 implementation 来自 [iq][1]，将平面划分为 Grid，对每一个像素，计算它所属的 Cell， 再计算它到以这个 Cell 为中心的 3x3 的 Cells 中的所有采样点的距离，最后取最小值即可，iq 的实现完全在 Domain Space 中，所以计算非常简单易懂，也易于应用到任何表面上。

Reference code:

```cpp
float voronoi(vec2 p)
{
    // the domain is partitioned in a grid.
    // find out which cell p is in.
    ivec2 n = floor(p);
    vec2 f = fract(p);
    
    float d = 8.0;
    
    // The set of 3x3 cells is examined
    // All the calculations are happening in the cell space
    // The origin is at the current cell
    for(int j = -1; j <= 1; ++j)
        for(int i = -1; i <= 1; ++i)
        {
            // b is the coordinates of the cell to be examined.
            ivec2 b = ivec2(i, j);
            
            // hash(p+b) is to get the position of the seed at cell b, 
            // it's in the range of [0,1], so it's the position in a cell.
            
            // b+hash(p+b) is vector from origin to the seed of cell b.
            // so r is the vector from current position to the seed of cell b.
            vec2 r = vec2(b) + hash(p + b) - f;
    
            float t = dot(r, r);
            
            // update the minimum distance
            d = min(d, t);
        }
    
    // d is now the minimum distance to all 3x3 cells.
    return sqrt(d);
}
```

<br>

<figure>
<img src="/assets/images/voronoi/01.png" alt="what"/>
<figcaption>Based on iq‘s Implementation。</figcaption>
</figure>
<br/>

这个算法可以很容易扩展到 3D，只需要加一层循环计算 3x3x3 的 Cells 即可，但是也因此，3D voronoi 非常慢，要想实时地在 3D 物体上做 Voronoi Displacement Mapping 的话，场景的复杂度就很受限制了。

## Fast Fake Voronoi
这个算法是来自于 [Shane][2]，之所以叫做 Fake Voronoi，是因为算法产生的并不满足 Voronoi Diagram 的定义，而仅仅是看起来像 Voronoi 的 Celluar Pattern。

<br>

<figure>
<img src="/assets/images/voronoi/02.png" alt="what"/>
<figcaption>Fake Voronoi。</figcaption>
</figure>
<br/>

算法的 Idea 基于在平面上重复绘制 Gradient Circle，再将每次绘制结果用 min 函数 blend 起来，看起来就像 Voronoi 图一样， 那么什么是 Gradient Circle 呢？ 就像下面这样：

<center><img src="/assets/images/voronoi/03.png"></center>
<br/>

然后我们把 Size 缩小，频率增大，加点偏移，再画另一组 Gradient Circle，像这样：

<center><img src="/assets/images/voronoi/04.png"></center>
<br/>

再把上面两次的结果，用 min(src, dst) 的混合方式叠加，就得到了：

<center><img src="/assets/images/voronoi/05.png"></center>
<br/>

看起来已经有点 Voronoi 的样子了，于是我们再以不同的 Size 或者偏移画另外几组 Gradient Circles，然后叠加在一起，就得到了基本看不出什么问题的 Voronoi：

<center><img src="/assets/images/voronoi/02.png"></center>
<br/>

Reference Code：

```cpp
float drawShape(in vec2 p)
{
    // Wrappable circle distance. The squared distance, to be more precise.
    p = fract(p)-.5;    
    return dot(p, p);
}

// Draw some cirlcles on a repeatable tile. The offsets were partly based on science, but
// for the most part, you could choose any combinations you want.
//
float cellTex(in vec2 p)
{   
    float c = .25; // Set the maximum, bearing in mind that it is multiplied by 4.
    
    // Draw four overlapping shapes (circles, in this case) using the darken blend 
    // at various positions on the tile.
    c = min(c, drawShape(p - vec2(.89, .62)));
    c = min(c, drawShape(p - vec2(.38, .20)));
    
    c = min(c, drawShape(p - vec2(.60, .24)));
    c = min(c, drawShape(p - vec2(.18, .82)));

    // Draw four smaller circles at various positions on the tile.
    
    p *= 1.4142; 
    //p = p.yx; // Extra option, or addition.
    
    c = min(c, drawShape(p - vec2(.46, .30)));
    c = min(c, drawShape(p - vec2(.04, .88))); 
    
    // More shapes produce a more convincing pattern, but you could cut
    // these two out and still produce a decent image.
    c = min(c, drawShape(p - vec2(.06, .54)));
    c = min(c, drawShape(p - vec2(.64, .12)));  
    
    return sqrt(c*4.);
}
```

这个算法的最大优势就是速度非常快，而且完全可以根据自己对精度的需求调整画 Gradient Circle 的次数，并且，即使扩展到 3D，也只需要简单地把 Gradient Circle 换成 Sphere 即可，在一些场景中，画三四次不同偏移的 Gradient Sphere 就已经够用了。


[1]:https://www.shadertoy.com/view/MslGD8
[2]:https://www.shadertoy.com/view/XtyGzc

